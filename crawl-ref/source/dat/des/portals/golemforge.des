###############################################################################
#
# The Forge of Golems (youredoomed)
#
###############################################################################

{{
function golemforge_portal(e)
  local timeout_turns = crawl.random_range(600, 800)

  local messager =
    timed_msg {
      initmsg = { "You hear a sound of hammering.",
                  "There is an entrance to the golemforge on this level. "
                  .. "Hurry and find it before the portal disappears!" },
      finalmsg = "A sound of hammering is finaly fades away.",
      verb = 'ringing',
      noisemaker = 'sound of hammering',
      ranges = {
        { 5000, 'faint ' },  { 4000, '' },
        { 2500, 'steady ' }, { 1500, 'quick ' },
        { 0, 'loud ' }
      }
    }

  e.lua_marker('O',
      timed_marker {
        disappear = "The gateway is sealed and disappears.",
        entity = 'gateway',
        turns = timeout_turns,
        single_timed = true,
        floor = "expired_portal",
        feat_tile = "dngn_portal_golem_gone",
        msg = messager })
  e.tags("uniq_golemforge chance_golemforge")
  e.chance(500)
  e.kfeat("O = enter_golem")
  e.kfeat("X = permarock_wall")
  e.kfeat("x = crystal_wall")
  e.rtile("x = dngn_crystal_lightblue")
  e.colour("X = blue")
  e.colour(".1234x = lightblue")
  e.ftile(".'12345O+xX = floor_crystal_squares")
end

function golemforge_appearance(e)
  e.kprop("aABbCcD+xXLrm = no_tele_into")
  e.kfeat("< = exit_golem")
  e.kfeat("X = permarock_wall")
  e.kfeat("x = crystal_wall")
  e.rtile("x = dngn_crystal_lightblue")
  e.colour("X = blue")
  e.colour(".x = lightblue")
  e.ftile(".aABbCcDO+xX = floor_crystal_squares")
end

}}
# Entries ##########################################
default-depth: Elf, Depths:1-4

NAME:   portal_golemforge_entry
TAGS:   transparent
DEPTH:  Elf, Depths:1-4
: golemforge_portal(_G)
: set_feature_name("crystal_wall", "wall of crystal")
MAP
     .
    x.x
  xxx.xxx
 xx.....xx
.....O.....
 xx.....xx
  xxx.xxx
    x.x
     .
ENDMAP

# The destination Depths:1-4s ############

default-depth: Golemforge

NAME:   golemforge_statue_garden_chambered
TAGS:   no_item_gen no_mons_gen no_pool_fixup
ORIENT: encompass
KMONS: y =crystal guardian
KMONS: i = iron golem
KMONS: G = iron golem col:lightred name:orange_crystal_golem n_rpl n_des n_spe \
            tile:mons_golemforge_crystal_golem \
            spells:brain_feed.24.natural;draining_gaze.24.natural
KMONS: P = iron golem col:green name:plutonium_golem n_rpl n_des n_spe \
            tile:mons_golemforge_plutonium_golem spells:corrupting_pulse.30.natural
KMONS: F = iron golem perm_ench:inner_flame col:red name:lava_golem n_rpl n_des n_spe \
            tile:mons_golemforge_lava_golem \
            spells:bolt_of_magma.24.natural;sticky_flame.100.natural.instant
KMONS: S = lightning spire hd:12 col:lightgreen name:repair_machine n_rpl n_des n_spe \
            tile:mons_golemforge_repair_machine \
            spells:aura_of_healing.70.magical;warning_cry.30.magical
KMONS: m = animated armour tile:mons_golemforge_armour_golem spells:none \
            ; double sword randart . buckler randart |\
            broad axe randart . shield randart |\
            eveningstar randart . large shield randart |\
            demon trident randart . javelin ego:silver q:4 |\
            lajatang randart . throwing net q:1 |\
            quick blade randart . dart ego:curare q:20 |\
            longbow randart. arrow q:40 |\
            triple crossbow randart . bolt q:40 |\
            fustibalus randart . sling bullet q:40 |\
            triple sword randart | bardiche randart |\
            executioner's axe randart | great mace randart .\
            crystal plate armour randart | robe randart |\
            fire dragon scales randart | ice dragon scales randart | storm dragon scales randart |\
            pearl dragon scales randart | swamp dragon scales randart | steam dragon scales randart |\
            acid dragon scales randart | shadow dragon scales randart | quicksilver dragon scales randart
SHUFFLE:  GP
FTILE:  iyGPSmAaBbCcD = floor_crystal_squares
COLOUR: iyGPSmAaBbCcD = lightblue
KFEAT:  Lm = lava
KFEAT:  A = stone_arch
KITEM: a = randbook disc:necromancy slevels:16 / randbook disc:conjuration slevels:16 / \
        randbook disc:summoning slevels:16 / randbook disc:charms slevels:16 / \
        randbook disc:hexes slevels:16 / randbook disc:translocation slevels:16 / \
        randbook disc:transmutation slevels:16 / randbook disc:fire slevels:16 / \
        randbook disc:air slevels:16 / randbook disc:earth slevels:16 / \
        randbook disc:ice slevels:16 / randbook disc:poison slevels:16
KITEM: B = triple sword good_item randart no_pickup / double sword good_item randart no_pickup / \
        great mace good_item randart no_pickup / eveningstar good_item randart no_pickup / \
        bardiche good_item randart no_pickup / demon trident good_item randart no_pickup / \
        executioner's axe good_item randart no_pickup / broad axe good_item randart no_pickup / \
        lajatang good_item randart no_pickup / quick blade good_item randart no_pickup / \
        longbow good_item randart no_pickup / triple crossbow good_item randart no_pickup / \
        fustibalus good_item randart no_pickup
KITEM: b = large rock q:20 no_pickup / javelin ego:silver q:20 no_pickup / \
        sling bullet q:100 no_pickup / dart ego:curare q:20 no_pickup
KITEM: C = crystal plate armour good_item randart no_pickup / \
        robe good_item randart no_pickup / \
        fire dragon scales good_item randart no_pickup / \
        ice dragon scales good_item randart no_pickup / \
        storm dragon scales good_item randart no_pickup / \
        pearl dragon scales good_item randart no_pickup / \
        swamp dragon scales good_item randart no_pickup / \
        steam dragon scales good_item randart no_pickup / \
        acid dragon scales good_item randart no_pickup / \
        shadow dragon scales good_item randart no_pickup / \
        quicksilver dragon scales good_item randart no_pickup
KITEM: c = pair of gloves good_item randart no_pickup / \
        pair of boots good_item randart no_pickup / \
        helmet good_item randart no_pickup / \
        cloak good_item randart no_pickup / \
        naga barding good_item randart no_pickup / \
        centaur barding good_item randart no_pickup / \
        large shield good_item randart no_pickup
KITEM: D = any randart jewellery no_pickup
: golemforge_appearance(_G)
: set_feature_name("crystal_wall", "wall of crystal")
MAP
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXxxxxxxxxxxxxxXXXXXXXXXXXXXXXXXXXXXXXXXxxxxxxxxxxxxxXXXXXXXXXX
XXXXXXxxxxx.........xxxXXXXXXXXXXXXXXXXXXXXXXxxxx.........xxxXXXXXXXXX
XXXXXXxA.............xxxxXXXXXXXXXXXXXXXXXXxxx.............xxxxXXXXXXX
XXXXxxx.................xxxXXXXXXXXXXXXXXXxxx........P........xxxXXXXX
XXxxx.................i...xxxXXXXXXXXXXXxxx...i.............i...xxxXXX
XXx..................ySy....xxxxxxxxxxxxx....ySy...........ySy....xXXX
XXx...................i.......................i.............i.....xXXX
XXx...........LLL...................................LLL...........xXXX
XXx..........LLLLL.................................LLLLL..........xXXX
XXx..........LLmLL................S................LLmLL....F.....xXXX
XXx..........LLLLL.................................LLLLL..........xXXX
XXx...........LLL...................................LLL...........xXXX
XXx.....i.............i..........L.L..........i.............i.....xXXX
XXx....ySy...........ySy....xxxxxx+xxxxxx....ySy...........ySy....xXXX
XXxxx...i.............i...xxxXXXXxmxXXXXxxx...i.............i...xxxXXX
XXXXxxx.................xxxXXXXXXxmxXXXXXXxxx........G........xxxXXXXX
XXXXXXxxx.............xxxXXXXXXXXxmxXXXXXXXXxxx.............xxxXXXXXXX
XXXXXXXXxxx.........xxxXXXXXXXXXXxmxXXXXXXXXXXxxx.........xxxXXXXXXXXX
XXXXXXXXXXxx.......xxxXXXXXXXXXxxxLxxxXXXXXXXXXxxx.......xxXXXXXXXXXXX
XXXXXXXXXXxx.......xXXXXXXXXXXxxLLLLLxxXXXXXXXXXXx.......xxXXXXXXXXXXX
XXXXXXXXXXxx.......xXXXXXXXXxxxLLxxxLLxxXXXXXXXXXx.......xxXXXXXXXXXXX
XXXXXXXxxxx.......ixXXXXXxxxxxLLxxXxxLLxxxxxXXXXXxi.......xxxxxXXXXXXX
XXXXXXxx..........LxxxxxxxxxxLLxxXXXxxLLxxxxxxxxxxL..........xxxXXXXXX
XXXXXXx.y..........+mmmmLLLLLLLxXXXXXxLLLLLLLmmmm+...........y.xXXXXXX
XXXXXXx..S........LxxxxxxxxxxLLxxXXXxxLLxxxxxxxxxxL.........S..xXXXXXX
XXXXXXx.y.........ixXXXXXXXXxxLLxxXxxLLxxxxxXXXXXxi..........y.xXXXXXX
XXXXXXxx...........xXXXXXXXXXxxLLxxxLLxxXXXXXXXXXx...........xxxXXXXXX
XXXXXXXxxx.........xxXXXXXXXXXxxLLLLLxxXXXXXXXXXxx..........xxxXXXXXXX
XXXXXXXXXxx.........xxxxXXXXXXXxxxLxxxXXXXXXXxxxx.........xxxXXXXXXXXX
XXXXXXXXXXxx...........xxXXXXXXxxmmmxxXXXXXXxx...........xxXXXXXXXXXXX
XXXXXXXXXXXx..........y.xXXXXXXxLmmmLxXXXXXXx.y..........xXXXXXXXXXXXX
XXXXXXXXXXXx..y......S..xXXXXXXxxmmmxxXXXXXXx..S......y..xXXXXXXXXXXXX
XXXXXXXXXXXx..........y.xXXXXXXxxxLxxxXXXXXXx.y..........xXXXXXXXXXXXX
XXXXXXXXXXxx...........xxXXXXXXxxxLxxxXXXXXXxx...........xxXXXXXXXXXXX
XXXXXXXXXxx.........xxxxXXXXXXXxxxLxxxXXXXXXXxxxx.........xxxXXXXXXXXX
XXXXXXXxxx.........xxXXXXXXXXXxxLLLLLxxXXXXXXXXXxx..........xxXXXXXXXX
XXXXXXxx...........xXXXXXXXXXxxLLxxxLLxxXXXXXXXXXx...........xxxXXXXXX
XXXXXXx.y.........ixXXXXXxxxxxLLxxXxxLLxxxxxXXXXXxi..........y.xXXXXXX
XXXXXXx..S........LxxxxxxxxxxLLxxXXXxxLLxxxxxxxxxxL.........S..xXXXXXX
XXXXXXx.y..........+mmmmLLLLLLLxXXXXXxLLLLLLLmmmm+...........y.xXXXXXX
XXXXXXxx..........LxxxxxxxxxxLLxxXXXxxLLxxxxxxxxxxL..........xxxXXXXXX
XXXXXXXxxxx.......ixXXXXXXXXxxLLxxXxxLLxxXXXXXXXXxi......xxxxxXXXXXXXX
XXXXXXXXXXxx.......xXXXXXXXXXxxLLxxxLLxxXXXXXXXXXx......xxXXXXXXXXXXXX
XXXXXXXXXXXx.......xXXXXXXXXXXxxLLLLLxxXXXXXXXXXXX......xXXXXXXXXXXXXX
XXXXXXXXXxxx.......xxxxxxXXXXXXxxxLxxxXXXXXXXxxxxx......xxXXXXXXXXXXXX
XXXXXXXXxxx.........xxxXXXXXXXXXXxmxXXXXXXXXXXxxx.........xxxXXXXXXXXX
XXXXXXxxx.............xxxXXXXXXXXxmxXXXXXXXXxxx.............xxxXXXXXXX
XXXXxxx........G........xxxXXXXXXxmxXXXXXXxxx........P........xxxXXXXX
XXxxx..y.i...........i.y..xxxXXXXxmxXXXXxxx..y.i...........i.y..xxxXXX
XXx.....S.............S.....xxxxxx+xxxxxx.....S.............S.....xXXX
XXx....i.y...........y.i........iL.Li........i.y...........y.i....xXXX
XXx...........LLL...................................LLL...........xXXX
XXx..........LLLLL.................................LLLLL..........xXXX
XXx....F.....LLmLL................S................LLmLL.....F....xXXX
XXx..........LLLLL.................................LLLLL..........xXXX
XXx....y.i....LLL....i.y.....................y.i....LLL...........xXXX
XXx.....S.............S.......................S...................xXXX
XXx....i.y...........y.i....xxxxxxxxxxxxx....i.y..................xXXX
XXxxx..........P..........xxxXXXXXXXXXXXxxx..........G..........xxxXXX
XXXXxxx.................xxxXXXXXXXXXXXXXXXxxx.................x+xXXXXX
XXXXXXxxx.............xxxXXXXXXXXXXXXXXXXXXXxxx.............xxx.xXXXXX
XXXXXXXXxxx.........xxxXXXXXXXXXXXXXXXXXXXXXXXxxx.........xxxXxCxXXXXX
XXXXXXXXXxxxxxxxxxxxxxXXXXXXXXXXXXXXXXXXXXXXXXXxxxxxxxxxxxxxXXxBxXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXxDxXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXxcxXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXxxxxxxxxxxxxxxxxxxxCxXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXx<DacBbDaCBaDcbBaBbcxXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXxxxxxxxxxxxxxxxxxxxxxXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXxXXXXXXXXXXXXXXXXXXXXXXXXX
ENDMAP